apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion sdk_compile

    signingConfigs {
        release {
            file("../signing.properties").with { propFile ->
                if (propFile.canRead()) {
                    def properties = new Properties()
                    properties.load(new FileInputStream(propFile))

                    storeFile file(properties['keystorePath'])
                    storePassword properties['keystorePassword']
                    keyAlias properties['keyAlias']
                    keyPassword properties['keyPassword']
                } else {
                    println 'Unable to read signing.properties'
                }
            }
        }
    }

    defaultConfig {
        applicationId "com.xsolla.android.storesdkexample"
        minSdkVersion sdk_min
        targetSdkVersion sdk_target
        versionCode sample_app_version_code
        versionName sample_app_version_name
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true

        buildConfigField 'int', 'PROJECT_ID', '58917' // Replace to your Project ID
        buildConfigField 'String', 'LOGIN_ID', '"ba55b4f6-babf-11ea-a85b-42010aa80004"' // Replace to your project's Login ID
        buildConfigField 'String', 'GOOGLE_CREDENTIAL', "\"${System.env.GOOGLE_SERVER_ID}\""
        buildConfigField 'boolean', 'WITH_LOGOUT', 'false'
        buildConfigField 'boolean', 'USE_OAUTH', 'true'
        buildConfigField 'int', 'OAUTH_CLIENT_ID', '58'

        buildConfigField "String", "VERSION_NAME", "\"$versionName\""
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField 'boolean', 'IS_SANDBOX', 'false'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField 'boolean', 'IS_SANDBOX', 'true'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    implementation project(":app-core")

    implementation project(":xsolla-login-sdk")
    implementation project(":xsolla-login-sdk-facebook")
    implementation project(":xsolla-login-sdk-google")
    implementation project(":xsolla-store-sdk")
    implementation project(":xsolla-inventory-sdk")
    implementation project(":xsolla-payments-sdk")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'
}
repositories {
    mavenCentral()
}
